plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.4'
}

// Configure Java compilation
sourceCompatibility = 21
targetCompatibility = 21

// Get version from property, CI environment variable, or default
def getPluginVersion() {
    return gradle.pluginVersion
}

def getPluginRevision() {
    def hash = findProperty('commitHash') ?: 
        System.getenv('GIT_COMMIT') ?:
        System.getenv('CI_COMMIT_SHORT_SHA') ?:
        System.getenv('GITHUB_SHA')?.take(8) ?:
        getGitCommitHash()
}

def getGitCommitHash() {
    try {
        def stdout = new ByteArrayOutputStream()
        def execResult = exec {
            commandLine 'git', 'rev-parse', '--is-inside-work-tree'
            standardOutput = stdout
            ignoreExitValue = true 
        }
        
        if (execResult.exitValue != 0) {
            println "Not in a git repository, using 'dev' as commit hash"
            return 'dev'
        }
        def hashLength = 8
        return "git rev-parse HEAD".execute().text.take(hashLength)
    } catch (Exception e) {
        return e
    }
}

def getGitTag() {
    try {
        // Check if we're in a git repository first
        def stdout = new ByteArrayOutputStream()
        def execResult = exec {
            commandLine 'git', 'rev-parse', '--is-inside-work-tree'
            standardOutput = stdout
            ignoreExitValue = true
        }
        
        if (execResult.exitValue != 0) {
            return null
        }
        
        // Check if current commit has a tag
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--exact-match', '--tags', 'HEAD'
            standardOutput = stdout
            ignoreExitValue = true
        }
        
        def tag = stdout.toString().trim()
        if (tag && !tag.isEmpty()) {
            println "Found git tag: ${tag}"
            return tag
        }
        return null
    } catch (Exception e) {
        return null
    }
}

def determineVersion() {
    // First check for CI/CD environment variables for tags
    def tag = System.getenv('CI_COMMIT_TAG') ?: 
              System.getenv('GIT_TAG') ?:
              getGitTag()
    
    if (tag) {
        // If we have a tag, use it directly
        println "Using git tag as version: ${tag}"
        return tag
    } else {
        // Otherwise use the original versioning scheme
        def version = "${getPluginVersion()}-${getPluginRevision()}"
        println "Using standard version: ${version}"
        return version
    }
}

group = 'net.phylax.credible'
version = determineVersion()

repositories {
    mavenCentral()
    maven {
        name = 'hyperledger'
        url = 'https://hyperledger.jfrog.io/artifactory/besu-maven/'
    }
}

dependencies {
    // Besu Plugin API and core dependencies
    compileOnly 'org.hyperledger.besu:besu-plugin-api:25.9.0'
    compileOnly 'org.hyperledger.besu:besu-evm:25.9.0'
    compileOnly 'org.hyperledger.besu:besu-datatypes:25.9.0'

    implementation 'io.consensys.tuweni:tuweni-units:2.7.1'
    
    // CLI Support
    compileOnly 'info.picocli:picocli:4.7.0'
    
    // HTTP Client
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'

    // JSON Processing (including JDK8 support)
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.18.2'

    // gRPC and Protobuf
    implementation 'io.grpc:grpc-netty-shaded:1.75.0'
    implementation 'io.grpc:grpc-protobuf:1.75.0'
    implementation 'io.grpc:grpc-stub:1.75.0'
    implementation 'com.google.protobuf:protobuf-java:4.32.1'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    
    // Caching (Caffeine)
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // OpenTelemetry SDK
    implementation 'io.opentelemetry:opentelemetry-sdk:1.32.0'
    
    // OTLP Exporter
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.32.0'
    
    // Semantic Conventions
    implementation 'io.opentelemetry.semconv:opentelemetry-semconv:1.32.0'
    
    // OkHttp Instrumentation
    implementation 'io.opentelemetry.instrumentation:opentelemetry-okhttp-3.0:2.20.0-alpha'
    implementation 'io.opentelemetry.instrumentation:opentelemetry-grpc-1.6:2.20.0-alpha'
    
    // Logging (including Lombok support)
    implementation 'org.slf4j:slf4j-api:1.7.36'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    // Auto Service
    implementation 'com.google.auto.service:auto-service:1.1.1'
    compileOnly 'com.google.auto.service:auto-service-annotations:1.1.1'
    
    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.mockito:mockito-core:5.+'
    testImplementation 'org.hyperledger.besu:besu-plugin-api:25.9.0'
    testImplementation 'org.hyperledger.besu:besu-evm:25.9.0'
    testImplementation 'org.hyperledger.besu:besu-datatypes:25.9.0'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'
    testImplementation 'io.grpc:grpc-testing:1.75.0'
    testImplementation 'io.grpc:grpc-inprocess:1.75.0'
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    systemProperty "log4j2.configurationFile", "src/test/resources/log4j.xml"
}

jar {
    manifest {
        attributes(
            'Implementation-Title': 'Credible Layer Plugin',
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Phylax',
            'Specification-Title': 'Besu Plugin API',
            'Specification-Version': '0.1.0',
            'Main-Class': 'net.phylax.credible.CredibleLayerPlugin'
        )
    }
    
    // Include all dependencies in fat jar (optional)
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    // Exclude signing files
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'credible-layer-besu-plugin'
            version = project.version
            
            pom {
                name = 'Credible Layer Besu Plugin'
                description = 'Hyperledger Besu plugin for integrating the Credible Sidecar'
                url = 'https://github.com/phylaxsystems/credible-layer-besu-plugin'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/phylaxsystems/credible-layer-besu-plugin"
            credentials {
                username = System.getenv("GH_ACTOR")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
}


// Custom tasks
task copyPlugin(type: Copy) {
    from jar
    into "${rootProject.projectDir}/build/plugins"
    doLast {
        println "Plugin JAR copied to: ${rootProject.projectDir}/build/plugins/${jar.archiveFileName.get()}"
    }
}

task installPlugin(type: Copy, dependsOn: jar) {
    from jar
    into System.getProperty('user.home') + '/.besu/plugins'
    doLast {
        println "Plugin installed to: ${System.getProperty('user.home')}/.besu/plugins/${jar.archiveFileName.get()}"
    }
}

build.finalizedBy copyPlugin

// Protobuf configuration
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:4.32.1'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.75.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Add generated sources to source sets
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}